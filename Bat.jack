// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Pong/Bat.jack
// (Same as projects/9/Pong/Bat.jack)
/**
 * A graphical bat in a Pong game.
 * Displayed as a filled horizontal rectangle that has a screen location,
 * a width and a height.
 */
class Bat {

    field int x, y;           // the bat's screen location
    field int width, height;  // the bat's width and height
    field int direction;      // direction of the bat's movement (1 = left, 2 = right)
    field int speed;          // pixels per move

    /** Constructs a new bat with the given location and width. */
    constructor Bat new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 2;
        let speed = 4;     // velocidade original do Pong
        do show();
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Shows the bat. */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** Hides the bat. */
    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    /** Draws the bat. */
    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    /** Sets the bat's direction (0=stop, 1=left, 2=right). */
    method void setDirection(int Adirection) {
        let direction = Adirection;
        return;
    }

    /** Returns the bat's left edge. */
    method int getLeft() {
        return x;
    }

    /** Returns the bat's right edge. */
    method int getRight() {
        return x + width;
    }

    /** Returns the bat's top edge. */
    method int getTop() {
        return y;
    }

    /** Returns the bat's bottom edge. */
    method int getBottom() {
        return y + height;
    }

    /** Sets the bat's width. */
    method void setWidth(int Awidth) {
        do hide();
        let width = Awidth;
        do show();
        return;
    }

    /** Moves the bat one step in the bat's direction. */
    method void move() {
        if (direction = 1) {
            let x = x - speed;
            if (x < 0) { let x = 0; }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + speed, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + (speed - 1), y + height);
        }
        else {
            if (direction = 2) {
                let x = x + speed;
                if ((x + width) > 511) { let x = 511 - width; }
                do Screen.setColor(false);
                do Screen.drawRectangle(x - speed, y, x - 1, y + height);
                do Screen.setColor(true);
                do Screen.drawRectangle((x + width) - (speed - 1), y, x + width, y + height);
            }
        }
        return;
    }
}

